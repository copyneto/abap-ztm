@Metadata.layer: #CORE
@UI: { headerInfo:           { typeName: 'Cockpit Prestação de Contas', typeNamePlural: 'Cockpit Prestação de Contas' },
       presentationVariant: [{ sortOrder: [{ by: 'FreightOrder', direction:  #DESC },
                                           { by: 'FreightUnit', direction:  #ASC }] }] }

@UI.lineItem: [{criticality: 'TranspOrdLifeCycleStatusCrit'}]

annotate view ZC_TM_COCKPIT_PRESTACAO_CONTAS with
{

  // ------------------------------------------------------
  // Header information
  // ------------------------------------------------------
  @UI.facet: [ { id:              'HeaderFreightOrder',
                 purpose:         #HEADER,
                 type:            #DATAPOINT_REFERENCE,
                 targetQualifier: 'HeaderFreightOrder',
                 position:        10 },

               { id:              'HeaderFreightUnit',
                 purpose:         #HEADER,
                 type:            #DATAPOINT_REFERENCE,
                 targetQualifier: 'HeaderFreightUnit',
                 position:        20 },

               { id:              'HeaderFreightStatus',
                 purpose:         #HEADER,
                 type:            #DATAPOINT_REFERENCE,
                 targetQualifier: 'HeaderFreightStatus',
                 position:        30 },

               { id:              'CABECALHO_DOCUMENTOS',
                 isSummary:       true,
                 type:            #COLLECTION,
                 label:           'Documentos',
                 position:        10 },

                        { parentId:        'CABECALHO_DOCUMENTOS',
                          id:              'CabDocs',
                          type:            #FIELDGROUP_REFERENCE,
                          label:           'Documentos',
                          position:        10,
                          targetQualifier: 'CabDocs' },

               { id:              'CABECALHO_STATUS',
                 isSummary:       true,
                 type:            #COLLECTION,
                 label:           'Execução',
                 position:        20 },

                        { parentId:        'CABECALHO_STATUS',
                          id:              'CabStat',
                          type:            #FIELDGROUP_REFERENCE,
                          label:           'Execução',
                          position:        10,
                          targetQualifier: 'CabStat' },

               { id:              'Execucao',
                 purpose:         #STANDARD,
                 type:            #LINEITEM_REFERENCE,
                 label:           'Histórico de Execução',
                 position:        30,
                 targetElement:   '_Execucao'},

               { id:              'CABECALHO_MOTORISTA',
                 isSummary:       true,
                 type:            #COLLECTION,
                 label:           'Motorista',
                 position:        40 },

                        { parentId:        'CABECALHO_MOTORISTA',
                          id:              'CabMoto',
                          type:            #FIELDGROUP_REFERENCE,
                          label:           'Motorista',
                          position:        10,
                          targetQualifier: 'CabMoto' },

               { id:              'CABECALHO_RECEBEDOR',
                 isSummary:       true,
                 type:            #COLLECTION,
                 label:           'Recebedor',
                 position:        50 },

                        { parentId:        'CABECALHO_RECEBEDOR',
                          id:              'CabRec',
                          type:            #FIELDGROUP_REFERENCE,
                          label:           'Recebedor',
                          position:        10,
                          targetQualifier: 'CabRec' },

               { id:              'CABECALHO_PAGAMENTO',
                 isSummary:       true,
                 type:            #COLLECTION,
                 label:           'Pagamento',
                 position:        60 },

                        { parentId:        'CABECALHO_PAGAMENTO',
                          id:              'CabPag',
                          type:            #FIELDGROUP_REFERENCE,
                          label:           'Pagamento',
                          position:        10,
                          targetQualifier: 'CabPag' }

               ]

  // ------------------------------------------------------
  // Buttons information
  // ------------------------------------------------------
  @UI.lineItem:         [ { position: 10, type: #FOR_ACTION, dataAction: 'novoStatusEvento', label: 'Inserir Evento' },
                          { position: 20, type: #FOR_ACTION, dataAction: 'definirEmProcessamento', label: 'Definir "Em processamento"' },
                          { position: 30, type: #FOR_ACTION, dataAction: 'definirConcluido', label: 'Definir "Concluído"' } ]

  @UI.identification:   [ { position: 10, type: #FOR_ACTION, dataAction: 'novoStatusEvento', label: 'Inserir Evento' },
                          { position: 20, type: #FOR_ACTION, dataAction: 'definirEmProcessamento', label: 'Definir "Em processamento"' },
                          { position: 30, type: #FOR_ACTION, dataAction: 'definirConcluido', label: 'Definir "Concluído"' } ]

  // ------------------------------------------------------
  // Field information
  // ------------------------------------------------------
  @UI.hidden: true
  FreightOrderUUID;

  @UI.hidden: true
  FreightUnitUUID;

  @Consumption.semanticObject:'FreightOrder'
  @UI: { lineItem:       [ { position: 70, label: 'Ordem de Frete', semanticObjectAction: 'displayRoad', type: #WITH_INTENT_BASED_NAVIGATION } ],
         dataPoint:        { qualifier: 'HeaderFreightOrder', title: 'Ordem de Frete'},
         fieldGroup:     [ { position: 10, label: 'Ordem de Frete', qualifier: 'CabDocs', semanticObjectAction: 'displayRoad', type: #WITH_INTENT_BASED_NAVIGATION } ],
         selectionField: [ { position: 10 } ] }
  FreightOrder;

  @UI: { lineItem:       [ { position: 80, label: 'Status Ordem de Frete', criticality: 'TranspOrdLifeCycleStatusCrit', criticalityRepresentation: #WITHOUT_ICON } ],
         dataPoint:        { qualifier: 'HeaderFreightStatus', title: 'Status', criticality: 'TranspOrdLifeCycleStatusCrit', criticalityRepresentation: #WITHOUT_ICON },
         selectionField: [ { position: 40 } ] }
  TranspOrdLifeCycleStatus;

  @UI.hidden: true
  TranspOrdLifeCycleStatusDesc;

  @Consumption.semanticObject:'FreightUnit'
  @UI: { lineItem:       [ { position: 90, label: 'Unidade de Frete', semanticObjectAction: 'display', type: #WITH_INTENT_BASED_NAVIGATION } ],
         dataPoint:        { qualifier: 'HeaderFreightUnit', title: 'Unidade de Frete'},
         fieldGroup:     [ { position: 20, label: 'Unidade de Frete', qualifier: 'CabDocs', semanticObjectAction: 'display', type: #WITH_INTENT_BASED_NAVIGATION } ] }
  FreightUnit;

  @UI: { lineItem:       [ { position: 100, label: 'Status Unidade de Frete', criticality: 'TranspOrdEventCodeCrit', criticalityRepresentation: #WITHOUT_ICON } ],
         selectionField: [ { position: 50 } ] }
  TranspOrdEventCode;

  @UI.hidden: true
  TranspOrdEventCodeDesc;
  
  @UI.hidden: true
  TranspOrdEventCodeCrit;

  @UI: { lineItem:       [ { position: 110, label: 'Ordem de Venda' } ],
         fieldGroup:     [ { position: 30, label: 'Ordem de Venda', qualifier: 'CabDocs' } ] }
  SalesDocument;

  @UI: { lineItem:       [ { position: 120, label: 'Entrega' } ],
         fieldGroup:     [ { position: 40, label: 'Entrega', qualifier: 'CabDocs' } ] }
  DeliveryDocument;

  @Consumption.semanticObject:'NotaFiscal'
  @UI: { lineItem:       [ { position: 130, label: 'Nota Fiscal', semanticObjectAction: 'zzdisplay', type: #WITH_INTENT_BASED_NAVIGATION } ],
         fieldGroup:     [ { position: 50, label: 'Nota Fiscal', qualifier: 'CabDocs', semanticObjectAction: 'zzdisplay', type: #WITH_INTENT_BASED_NAVIGATION } ] }
  BR_NotaFiscal;

  @UI.hidden: true
  DriverUUID;

  @UI: { lineItem:       [ { position: 140, label: 'Motorista' } ],
         fieldGroup:     [ { position: 10, label: 'Motorista', qualifier: 'CabMoto' } ],
         selectionField: [ { position: 20 } ] }
  DriverId;

  @UI.hidden: true
  DriverName;

  @UI: { lineItem:       [ { position: 150, label: 'Placa do Veículo' } ],
         fieldGroup:     [ { position: 20, label: 'Placa do Veículo', qualifier: 'CabMoto' } ],
         selectionField: [ { position: 30 } ] }
  PlateNumber;

  @UI: { lineItem:       [ { position: 160, label: 'Tipo de Ordem de Venda' } ],
         fieldGroup:     [ { position: 10, label: 'Tipo de Ordem de Venda', qualifier: 'CabPag' } ] }
  SalesDocumentType;

  @UI: { lineItem:       [ { position: 170, label: 'Recebedor' } ],
         fieldGroup:     [ { position: 20, label: 'Recebedor', qualifier: 'CabRec' } ] }
  ConsigneeId;

  @UI.hidden: true
  ConsigneeName;

  @UI.hidden: true
  TranspOrdLifeCycleStatusCrit;

  @UI: { lineItem:       [ { importance: #HIGH, position: 10, label: 'Entregue', criticality: 'StatusEntregueCriticality' } ],
         fieldGroup:     [ { position: 10, label: 'Entregue', qualifier: 'CabStat', criticality: 'StatusEntregueCriticality' } ],
         textArrangement: #TEXT_ONLY }
  StatusEntregue;

  @UI.hidden: true
  StatusEntregueText;
  
  @UI.hidden: true
  StatusEntregueCriticality;

  @UI: { lineItem:       [ {  position: 20, label: 'Devolvido', criticality: 'StatusDevolvidoCriticality' } ],
         fieldGroup:     [ { position: 20, label: 'Devolvido', qualifier: 'CabStat', criticality: 'StatusDevolvidoCriticality' } ],
         textArrangement: #TEXT_ONLY }
  StatusDevolvido;

  @UI.hidden: true
  StatusDevolvidoText;

  @UI.hidden: true
  StatusDevolvidoCriticality;

  @UI: { lineItem:       [ { position: 30, label: 'Pendente', criticality: 'StatusPendenteCriticality' } ],
         fieldGroup:     [ { position: 30, label: 'Pendente', qualifier: 'CabStat', criticality: 'StatusPendenteCriticality' } ],
         textArrangement: #TEXT_ONLY }
  StatusPendente;

  @UI.hidden: true
  StatusPendenteText;

  @UI.hidden: true
  StatusPendenteCriticality;

  @UI: { lineItem:       [ { position: 40, label: 'Sinistro', criticality: 'StatusSinistroCriticality' } ],
         fieldGroup:     [ { position: 40, label: 'Sinistro', qualifier: 'CabStat', criticality: 'StatusSinistroCriticality' } ],
         textArrangement: #TEXT_ONLY }
  StatusSinistro;

  @UI.hidden: true
  StatusSinistroText;

  @UI.hidden: true
  StatusSinistroCriticality;

  @UI: { lineItem:       [ { position: 50, label: 'Coletado', criticality: 'StatusColetadoCriticality' } ],
         fieldGroup:     [ { position: 50, label: 'Coletado', qualifier: 'CabStat', criticality: 'StatusColetadoCriticality' } ],
         textArrangement: #TEXT_ONLY }
  StatusColetado;

  @UI.hidden: true
  StatusColetadoText;

  @UI.hidden: true
  StatusColetadoCriticality;

  @UI: { lineItem:       [ { position: 60, label: 'Não Coletado', criticality: 'StatusNaoColetadoCriticality' } ],
         fieldGroup:     [ { position: 60, label: 'Não Coletado', qualifier: 'CabStat', criticality: 'StatusNaoColetadoCriticality' } ],
         textArrangement: #TEXT_ONLY }
  StatusNaoColetado;

  @UI.hidden: true
  StatusNaoColetadoText;

  @UI.hidden: true
  StatusNaoColetadoCriticality;

  @UI.hidden: true
  BusinessPartnerUUID;

  @UI.hidden: true
  BusinessPartner;

  @UI: { lineItem:       [ { position: 180, label: 'Destino' } ],
         fieldGroup:     [ { position: 30, label: 'Destino', qualifier: 'CabRec' } ] }
  LocationDescription;

  @UI: { lineItem:       [ { position: 190, label: 'Tipo de Destino' } ],
         fieldGroup:     [ { position: 40, label: 'Tipo de Destino', qualifier: 'CabRec' } ] }
  LocationType;

  @UI: { lineItem:       [ { position: 200, label: 'País' } ],
         fieldGroup:     [ { position: 50, label: 'País', qualifier: 'CabRec' } ] }
  Country;

  @UI: { lineItem:       [ { position: 210, label: 'Cidade' } ],
         fieldGroup:     [ { position: 60, label: 'Cidade', qualifier: 'CabRec' } ] }
  CityName;

  @UI: { lineItem:       [ { position: 220, label: 'CEP' } ],
         fieldGroup:     [ { position: 70, label: 'CEP', qualifier: 'CabRec' } ] }
  PostalCode;

  @UI: { lineItem:       [ { position: 230, label: 'Rua' } ],
         fieldGroup:     [ { position: 80, label: 'Rua', qualifier: 'CabRec' } ] }
  StreetName;

  @UI: { lineItem:       [ { position: 240, label: 'Número' } ],
         fieldGroup:     [ { position: 90, label: 'Número', qualifier: 'CabRec' } ] }
  HouseNumber;

  @UI: { lineItem:       [ { position: 250, label: 'Forma de Pagamento' } ],
         fieldGroup:     [ { position: 20, label: 'Forma de Pagamento', qualifier: 'CabPag' } ] }
  PaymentMethod;

  @UI.hidden: true
  PaymentMethodDescription;

  @UI: { lineItem:       [ { position: 260, label: 'Volumes' } ],
         fieldGroup:     [ { position: 30, label: 'Volumes', qualifier: 'CabPag' } ] }
  BaseValue;

  @UI: { lineItem:       [ { position: 270, label: 'Valor total' } ],
         fieldGroup:     [ { position: 40, label: 'Valor total', qualifier: 'CabPag' } ] }
  AmountValue;
  
  @Consumption.filter.hidden: true
  TranspOrdEventCodeNew;

}